buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.2.0'
    }
}

plugins {
    id "org.sonarqube" version "2.1-rc1"
    id "org.xbib.gradle.plugin.jbake" version "1.1.0"
}

println "Host: " + java.net.InetAddress.getLocalHost()
println "Gradle: " + gradle.gradleVersion + " JVM: " + org.gradle.internal.jvm.Jvm.current() + " Groovy: " + GroovySystem.getVersion()
println "Build: group: '${project.group}', name: '${project.name}', version: '${project.version}'"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: "jacoco"
apply plugin: 'org.ajoberstar.github-pages'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "http://xbib.org/repository"
    }
}

configurations {
    wagon
    provided
    testCompile.extendsFrom(provided)
}

dependencies {
    compile 'org.xbib:marc:1.0.2'
    compile "org.xbib:rdf:2+"
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.logging.log4j:log4j-jul:2.5'
    provided "org.xbib:bibliographic-character-sets:1.0.0"
    wagon 'org.apache.maven.wagon:wagon-ssh-external:2.10'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
}
test {
    classpath += configurations.provided
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
}
tasks.withType(FindBugs) {
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = false
    }
}
tasks.withType(Pmd) {
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
tasks.withType(Checkstyle) {
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        xml.destination "${buildDir}/reports/jacoco-xml"
        html.destination "${buildDir}/reports/jacoco-html"
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "xbib Catalog Entities"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.tests", "src/test/java"
        property "sonar.scm.provider", "git"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.junit.reportsPath", "build/test-results/test/"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
}
artifacts {
    archives sourcesJar, javadocJar
}
if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}
task xbibUpload(type: Upload) {
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        if (project.hasProperty("xbibUsername")) {
            mavenDeployer {
                configuration = configurations.wagon
                repository(url: uri('scpexe://xbib.org/repository')) {
                    authentication(userName: xbibUsername, privateKey: xbibPrivateKey)
                }
            }
        }
    }
}

githubPages {
    repoUri = 'git@github.com:xbib/catalog-entities.git'
    targetBranch = "gh-pages"
    pages {
        from(file('build/jbake')) {
            into '.'
        }
    }
}
